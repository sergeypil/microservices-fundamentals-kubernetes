plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.springframework.cloud.contract" version "4.1.3"
}

group = 'net.serg'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.0")
	cucumberVersion = "7.15.0"
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
	integrationTest {
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
		java.srcDir file('src/integrationTest/java')
		resources.srcDir file('src/integrationTest/resources')
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
	implementation 'ch.qos.logback:logback-core:1.5.6'
	implementation 'ch.qos.logback:logback-classic:1.5.6'
	implementation 'io.opentelemetry:opentelemetry-exporter-zipkin'
	implementation 'io.micrometer:micrometer-tracing-bridge-brave'
	implementation 'io.zipkin.reporter2:zipkin-reporter-brave'

	implementation 'org.postgresql:postgresql:42.7.1'
	implementation 'com.h2database:h2:2.2.224'
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'

	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:postgresql:1.19.5'
	testImplementation 'org.testcontainers:junit-jupiter'
	
	integrationTestImplementation 'org.springframework.boot:spring-boot-testcontainers'
	integrationTestImplementation 'org.testcontainers:postgresql:1.19.5'
	integrationTestImplementation 'org.testcontainers:junit-jupiter'

	implementation "io.cucumber:cucumber-java:$cucumberVersion"
	implementation "io.cucumber:cucumber-spring:$cucumberVersion"
	testImplementation "io.cucumber:cucumber-junit-platform-engine:$cucumberVersion"
	testImplementation 'org.junit.platform:junit-platform-suite:1.10.2'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

contracts {
	basePackageForTests = 'net.serg.songservice'
	baseClassForTests = 'net.serg.songservice.ContractVerifierBase'
}

task integrationTest(type: Test) {
	description = "Run integration tests"
	group = "verification"
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest